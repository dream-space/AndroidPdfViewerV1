plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.github.barteksc.pdfviewer'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        multiDexEnabled true
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'GradleDependency', 'ExpiringTargetSdkVersion'
    }
}
dependencies {
    implementation 'com.github.barteksc:pdfium-android:1.5.0'
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/dream-space/AndroidPdfViewerV1")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = "dream-space"
                artifactId = "AndroidPdfViewerV1"
                version = "1.6.0"
            }
        }
    }
}